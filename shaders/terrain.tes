#version 420 core

layout(quads, fractional_odd_spacing, cw) in;

// Input from Tessellation Control Shader
in vec3       LocalPos_TC_out[];
in vec2       TexCoords_TC_out[];
flat in float TextureSlice_TC_out[];
flat in vec3  WorldOffset_TC_out[];
in float      perturbFactor_TC_out[];
in float      tessFactor_TC_out[];

// Output to Fragment Shader
out vec3  FragPos;
out vec4  CurPosition;
out vec4  PrevPosition;
out vec3  Normal;
out vec2  TexCoords;
out float perturbFactor;
out float tessFactor;

// Heightmap texture array: R=height, GBA=normal
uniform sampler2DArray uHeightmap;
uniform int            uChunkSize;

uniform mat4 view;
uniform mat4 projection;
uniform vec4 clipPlane;

#include "helpers/shockwave.glsl"
#include "helpers/terrain_noise.glsl"
#include "temporal_data.glsl"

// Sample heightmap to get world position and normal
void sampleHeightmap(vec2 uv, float slice, vec3 worldOffset, out vec3 worldPos, out vec3 normal) {
	// vec2 uv_map = (uv * uChunkSize + 0.5) / (uChunkSize + 1.0);
	vec4  data = texture(uHeightmap, vec3(uv, slice));
	float height = data.r;
	normal = normalize(data.gba);

	worldPos.x = worldOffset.x + uv.x * float(uChunkSize);
	worldPos.y = height;
	worldPos.z = worldOffset.z + uv.y * float(uChunkSize);
}

void main() {
	vec2 uv = gl_TessCoord.xy;

	// Get instance data (same for all corners)
	float slice = TextureSlice_TC_out[0];
	vec3  worldOffset = WorldOffset_TC_out[0];

	// Interpolate texture coordinates for this tessellated vertex
	vec2 texCoord1 = mix(TexCoords_TC_out[0], TexCoords_TC_out[1], uv.x);
	vec2 texCoord2 = mix(TexCoords_TC_out[3], TexCoords_TC_out[2], uv.x);
	TexCoords = mix(texCoord1, texCoord2, uv.y);

	// Sample heightmap at the interpolated UV to get world position and normal
	vec3 q, n;
	sampleHeightmap(TexCoords, slice, worldOffset, FragPos, Normal);

	// Apply shockwave ripple to terrain
	FragPos += getShockwaveDisplacement(FragPos, 0.0, false);

	// Clip plane for reflections
	gl_ClipDistance[0] = dot(FragPos, clipPlane.xyz) + clipPlane.w;

	// How much to perturb normal
	float perturbFactor1 = mix(perturbFactor_TC_out[0], perturbFactor_TC_out[1], uv.x);
	float perturbFactor2 = mix(perturbFactor_TC_out[3], perturbFactor_TC_out[2], uv.x);
	perturbFactor = mix(perturbFactor1, perturbFactor2, uv.y);

	float tessFactor1 = mix(tessFactor_TC_out[0], tessFactor_TC_out[1], uv.x);
	float tessFactor2 = mix(tessFactor_TC_out[3], tessFactor_TC_out[2], uv.x);
	tessFactor = mix(tessFactor1, tessFactor2, uv.y);

	// out float tessFactor;

	// Final vertex position
	gl_Position = projection * view * vec4(FragPos, 1.0);

	CurPosition = gl_Position;
	PrevPosition = prevViewProjection * vec4(FragPos, 1.0);
}
