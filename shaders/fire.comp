#version 430 core

// Must match the C++ struct layout in fire_effect.h
struct Particle {
	vec4 pos; // Position (w is lifetime)
	vec4 vel; // Velocity (w is unused)
	int  style;
	int  emitter_index; // Index of the emitter that spawned this particle
	vec2 _padding;      // Explicit padding to align to 16 bytes
};

// Must match the C++ Emitter struct in fire_effect_manager.h
struct Emitter {
	vec3  position;
	int   style;
	vec3  direction;
	int   is_active;
	vec3  velocity;
	float _padding2;
};

layout(std430, binding = 0) buffer ParticleBuffer {
	Particle particles[];
};

layout(std430, binding = 1) buffer EmitterBuffer {
	Emitter emitters[];
};

layout(std430, binding = 2) buffer IndirectionBuffer {
	int particle_to_emitter_map[];
};

uniform float u_delta_time;
uniform float u_time;
uniform int   u_num_emitters;

// Basic pseudo-random number generator
float rand(vec2 co) {
	return fract(sin(dot(co.xy, vec2(12.9898, 78.233))) * 43758.5453);
}

// 3D random number generator
vec3 rand3(vec2 co) {
	return vec3(rand(co + vec2(0.1, 0.2)), rand(co + vec2(0.3, 0.4)), rand(co + vec2(0.5, 0.6)));
}

vec3 mod289(vec3 x) {
	return x - floor(x * (1.0 / 289.0)) * 289.0;
}

vec4 mod289(vec4 x) {
	return x - floor(x * (1.0 / 289.0)) * 289.0;
}

vec4 permute(vec4 x) {
	return mod289(((x * 34.0) + 1.0) * x);
}

vec4 taylorInvSqrt(vec4 r) {
	return 1.79284291400159 - 0.85373472095314 * r;
}

float snoise(vec3 v) {
	const vec2 C = vec2(1.0 / 6.0, 1.0 / 3.0);
	const vec4 D = vec4(0.0, 0.5, 1.0, 2.0);

	// First corner
	vec3 i = floor(v + dot(v, C.yyy));
	vec3 x0 = v - i + dot(i, C.xxx);

	// Other corners
	vec3 g = step(x0.yzx, x0.xyz);
	vec3 l = 1.0 - g;
	vec3 i1 = min(g.xyz, l.zxy);
	vec3 i2 = max(g.xyz, l.zxy);

	vec3 x1 = x0 - i1 + C.xxx;
	vec3 x2 = x0 - i2 + C.yyy;
	vec3 x3 = x0 - D.yyy;

	// Permutations
	i = mod289(i);
	vec4 p = permute(
		permute(permute(i.z + vec4(0.0, i1.z, i2.z, 1.0)) + i.y + vec4(0.0, i1.y, i2.y, 1.0)) + i.x +
		vec4(0.0, i1.x, i2.x, 1.0)
	);

	float n_ = 0.142857142857;
	vec3  ns = n_ * D.wyz - D.xzx;

	vec4 j = p - 49.0 * floor(p * ns.z * ns.z);

	vec4 x_ = floor(j * ns.z);
	vec4 y_ = floor(j - 7.0 * x_);

	vec4 x = x_ * ns.x + ns.yyyy;
	vec4 y = y_ * ns.x + ns.yyyy;
	vec4 h = 1.0 - abs(x) - abs(y);

	vec4 b0 = vec4(x.xy, y.xy);
	vec4 b1 = vec4(x.zw, y.zw);

	vec4 s0 = floor(b0) * 2.0 + 1.0;
	vec4 s1 = floor(b1) * 2.0 + 1.0;
	vec4 sh = -step(h, vec4(0.0));

	vec4 a0 = b0.xzyw + s0.xzyw * sh.xxyy;
	vec4 a1 = b1.xzyw + s1.xzyw * sh.zzww;

	vec3 p0 = vec3(a0.xy, h.x);
	vec3 p1 = vec3(a0.zw, h.y);
	vec3 p2 = vec3(a1.xy, h.z);
	vec3 p3 = vec3(a1.zw, h.w);

	// Normalise gradients
	vec4 norm = taylorInvSqrt(vec4(dot(p0, p0), dot(p1, p1), dot(p2, p2), dot(p3, p3)));
	p0 *= norm.x;
	p1 *= norm.y;
	p2 *= norm.z;
	p3 *= norm.w;

	// Mix final noise value
	vec4 m = max(0.6 - vec4(dot(x0, x0), dot(x1, x1), dot(x2, x2), dot(x3, x3)), 0.0);
	m = m * m;
	return 42.0 * dot(m * m, vec4(dot(p0, x0), dot(p1, x1), dot(p2, x2), dot(p3, x3)));
}

// Calculate Curl Noise by sampling the potential field derivatives
vec3 curlNoise(vec3 p, float time) {
	const float e = 0.1; // Epsilon for finite difference

	// We create a vector potential (psi) by sampling noise at offsets.
	// We animate it by adding time to the input.
	vec3 dx = vec3(e, 0.0, 0.0);
	vec3 dy = vec3(0.0, e, 0.0);
	vec3 dz = vec3(0.0, 0.0, e);

	// Helper to sample our potential field (just 3 arbitrary noise lookups)
	// We use large offsets (100.0) to decorrelate the axes
	vec3 p_t = p + vec3(0, 0, time * 0.5);

	float x0 = snoise(p_t - dy);
	float x1 = snoise(p_t + dy);
	float x2 = snoise(p_t - dz);
	float x3 = snoise(p_t + dz);

	float y0 = snoise(p_t - dz);
	float y1 = snoise(p_t + dz);
	float y2 = snoise(p_t - dx);
	float y3 = snoise(p_t + dx);

	float z0 = snoise(p_t - dx);
	float z1 = snoise(p_t + dx);
	float z2 = snoise(p_t - dy);
	float z3 = snoise(p_t + dy);

	// Finite difference approximation of curl
	float cx = (x1 - x0) - (y1 - y0); // dPsi_z/dy - dPsi_y/dz (simplified mapping)
	float cy = (y1 - y0) - (z1 - z0);
	float cz = (z1 - z0) - (x1 - x0);

	return normalize(vec3(cx, cy, cz));
}

// Simulation parameters
const float kExhaustSpeed = 30.0;
const float kExhaustSpread = 0.1;
const float kExhaustLifetime = 2.0;
const float kExhaustDrag = 1.5;

const float kExplosionSpeed = 30.0;
const float kExplosionLifetime = 2.5;
const float kExplosionDrag = 2.0;

const float kFireSpeed = 1.0;
const float kFireSpread = 4.2;
const float kFireLifetime = 5.0;

const float kMushroomCloudInitialSpeed = 45.0;
const float kMushroomCloudRiseSpeed = 15.0;
const float kMushroomCloudLifetime = 10.0;
const float kMushroomCloudDrag = 1.0;
const float kMushroomCloudExpansionFactor = 2.0;

layout(local_size_x = 256, local_size_y = 1, local_size_z = 1) in;

void main() {
	uint gid = gl_GlobalInvocationID.x;

	if (gid >= particles.length() || u_num_emitters == 0) {
		return;
	}

	Particle p = particles[gid];

	// Decrease lifetime
	p.pos.w -= u_delta_time;

	// If particle is dead, try to respawn it
	if (p.pos.w <= 0.0) {
		// Look up the assigned emitter from the indirection map
		int emitter_index = particle_to_emitter_map[gid];

		// If the emitter index is -1, this particle is inactive
		if (emitter_index == -1 || emitter_index >= u_num_emitters) {
			p.pos.w = 0.0; // Ensure it stays dead
			particles[gid] = p;
			return;
		}

		Emitter emitter = emitters[emitter_index];
		if (emitter.is_active == 0) {
			p.pos.w = 0.0; // Emitter is no longer active
			particles[gid] = p;
			return;
		}

		p.emitter_index = emitter_index;
		p.style = emitter.style;

		vec2 seed = vec2(float(gid), u_time);
		vec3 random_vec = rand3(seed) * 2.0 - 1.0;

		float lifeTimeModifier = rand(seed); // * 0.25 + 0.75;
		// Initialize based on style
		if (p.style == 0) { // MissileExhaust
			p.pos = vec4(emitter.position, kExhaustLifetime * lifeTimeModifier);
			vec3 perpendicular = normalize(cross(emitter.direction, random_vec));
			vec3 spread_dir = mix(emitter.direction, perpendicular, kExhaustSpread * rand(seed.yx));
			p.vel = vec4(emitter.velocity + normalize(spread_dir) * kExhaustSpeed * rand(seed), 0.0);
		} else if (p.style == 1) { // Explosion
			p.pos = vec4(emitter.position, kExplosionLifetime * lifeTimeModifier);
			p.vel = vec4(normalize(random_vec) * kExplosionSpeed * 0.5 + rand(seed.yx), 0.0);
		} else if (p.style == 2) { // Fire
			p.pos = vec4(emitter.position + random_vec * 0.1, kFireLifetime * lifeTimeModifier);
			p.vel = vec4(0, kFireSpeed, 0, 0); // Start with upward velocity
		} else if (p.style == 3) { // MushroomCloud
			p.pos = vec4(emitter.position, kMushroomCloudLifetime * lifeTimeModifier);
			vec3 explosion_dir = normalize(vec3(random_vec.x, abs(random_vec.y), random_vec.z));
			p.vel = vec4(explosion_dir * kMushroomCloudInitialSpeed * (0.5 + rand(seed.yx) * 0.5), 0.0);
		}
	}
	// If particle is alive, update it
	else {
		if (p.emitter_index >= u_num_emitters) {
			p.pos.w = 0.0;
		}
		float maxSpeed;
		if (p.style == 0) {
			maxSpeed = kExhaustSpeed;
			p.vel.xyz -= p.vel.xyz * kExhaustDrag * 2 * (1 - (p.vel.xyz / 30)) * u_delta_time;
		} else if (p.style == 1) { // Explosion
			maxSpeed = kExplosionSpeed;
			p.vel.xyz -= p.vel.xyz * kExplosionDrag * u_delta_time;
		} else if (p.style == 2) { // Fire
			maxSpeed = kFireSpeed;
			p.vel.y += kFireSpeed * u_delta_time;
			p.vel.x += (rand(p.pos.xy + u_time) - 0.5) * kFireSpread * u_delta_time;
			p.vel.z += (rand(p.pos.yz + u_time) - 0.5) * kFireSpread * u_delta_time;
		} else if (p.style == 3) { // MushroomCloud
			maxSpeed = kMushroomCloudInitialSpeed;
			float lifetime_progress = 1.0 - (p.pos.w / kMushroomCloudLifetime);

			// Rising stem
			p.vel.y += kMushroomCloudRiseSpeed * (1.0 - lifetime_progress) * u_delta_time;

			// Expanding cap
			if (lifetime_progress > 0.2) {
				vec3 center_to_particle = p.pos.xyz - emitters[p.emitter_index].position;
				p.vel.xz += normalize(center_to_particle.xz) * kMushroomCloudExpansionFactor * (lifetime_progress - 0.2) * u_delta_time;
			}
			p.vel.xyz -= p.vel.xyz * kMushroomCloudDrag * u_delta_time;
		}
		p.vel.y -= 0.05 * u_delta_time;

		p.vel += vec4(mix(curlNoise(p.pos.xyz, u_time) * 3, vec3(0, 0, 0), length(p.vel) / maxSpeed), 0) * u_delta_time;

		p.pos.xyz += p.vel.xyz * u_delta_time;

		if (p.pos.y <= 0 && p.pos.w > 0) {
			p.pos.y = 0.1;
			p.vel.xz += p.vel.y * vec2(0.5, 0.5);
			p.vel.y *= -0.25;
		}
	}

	particles[gid] = p;
}
