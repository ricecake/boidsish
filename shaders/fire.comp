#version 430 core

// Must match the C++ struct layout in fire_effect.h
struct Particle {
	vec4 pos; // Position (w is lifetime)
	vec4 vel; // Velocity (w is unused)
	vec3 epicenter;
	int  style;
	int  emitter_index;
	int  emitter_id;
	int  _padding[2];
};

// Must match the C++ Emitter struct in fire_effect_manager.h
struct Emitter {
	vec3  position;
	int   style;
	vec3  direction;
	int   is_active;
	vec3  velocity;
	int   id;
};

layout(std430, binding = 0) buffer ParticleBuffer {
	Particle particles[];
};

layout(std430, binding = 1) buffer EmitterBuffer {
	Emitter emitters[];
};

layout(std430, binding = 2) buffer IndirectionBuffer {
	int particle_to_emitter_map[];
};

struct ChunkInfo {
	vec2  worldOffset;
	float slice;
	float size;
};

layout(std430, binding = 3) buffer ChunkInfoBuffer {
	ChunkInfo chunks[];
};

uniform float u_delta_time;
uniform float u_time;
uniform int   u_num_emitters;
uniform int   u_num_chunks;

uniform sampler3D      u_curlTexture;
uniform sampler2DArray u_heightmapArray;

// Basic pseudo-random number generator
float rand(vec2 co) {
	return fract(sin(dot(co.xy, vec2(12.9898, 78.233))) * 43758.5453);
}

// 3D random number generator
vec3 rand3(vec2 co) {
	return vec3(rand(co + vec2(0.1, 0.2)), rand(co + vec2(0.3, 0.4)), rand(co + vec2(0.5, 0.6)));
}

vec3 mod289(vec3 x) {
	return x - floor(x * (1.0 / 289.0)) * 289.0;
}

vec4 mod289(vec4 x) {
	return x - floor(x * (1.0 / 289.0)) * 289.0;
}

vec4 permute(vec4 x) {
	return mod289(((x * 34.0) + 1.0) * x);
}

vec4 taylorInvSqrt(vec4 r) {
	return 1.79284291400159 - 0.85373472095314 * r;
}

float snoise(vec3 v) {
	const vec2 C = vec2(1.0 / 6.0, 1.0 / 3.0);
	const vec4 D = vec4(0.0, 0.5, 1.0, 2.0);

	// First corner
	vec3 i = floor(v + dot(v, C.yyy));
	vec3 x0 = v - i + dot(i, C.xxx);

	// Other corners
	vec3 g = step(x0.yzx, x0.xyz);
	vec3 l = 1.0 - g;
	vec3 i1 = min(g.xyz, l.zxy);
	vec3 i2 = max(g.xyz, l.zxy);

	vec3 x1 = x0 - i1 + C.xxx;
	vec3 x2 = x0 - i2 + C.yyy;
	vec3 x3 = x0 - D.yyy;

	// Permutations
	i = mod289(i);
	vec4 p = permute(
		permute(permute(i.z + vec4(0.0, i1.z, i2.z, 1.0)) + i.y + vec4(0.0, i1.y, i2.y, 1.0)) + i.x +
		vec4(0.0, i1.x, i2.x, 1.0)
	);

	float n_ = 0.142857142857;
	vec3  ns = n_ * D.wyz - D.xzx;

	vec4 j = p - 49.0 * floor(p * ns.z * ns.z);

	vec4 x_ = floor(j * ns.z);
	vec4 y_ = floor(j - 7.0 * x_);

	vec4 x = x_ * ns.x + ns.yyyy;
	vec4 y = y_ * ns.x + ns.yyyy;
	vec4 h = 1.0 - abs(x) - abs(y);

	vec4 b0 = vec4(x.xy, y.xy);
	vec4 b1 = vec4(x.zw, y.zw);

	vec4 s0 = floor(b0) * 2.0 + 1.0;
	vec4 s1 = floor(b1) * 2.0 + 1.0;
	vec4 sh = -step(h, vec4(0.0));

	vec4 a0 = b0.xzyw + s0.xzyw * sh.xxyy;
	vec4 a1 = b1.xzyw + s1.xzyw * sh.zzww;

	vec3 p0 = vec3(a0.xy, h.x);
	vec3 p1 = vec3(a0.zw, h.y);
	vec3 p2 = vec3(a1.xy, h.z);
	vec3 p3 = vec3(a1.zw, h.w);

	// Normalise gradients
	vec4 norm = taylorInvSqrt(vec4(dot(p0, p0), dot(p1, p1), dot(p2, p2), dot(p3, p3)));
	p0 *= norm.x;
	p1 *= norm.y;
	p2 *= norm.z;
	p3 *= norm.w;

	// Mix final noise value
	vec4 m = max(0.6 - vec4(dot(x0, x0), dot(x1, x1), dot(x2, x2), dot(x3, x3)), 0.0);
	m = m * m;
	return 42.0 * dot(m * m, vec4(dot(p0, x0), dot(p1, x1), dot(p2, x2), dot(p3, x3)));
}

// Calculate Curl Noise by sampling the pre-calculated curl texture
vec3 curlNoise(vec3 p, float time) {
	float noiseScale = 0.02;
	vec3  lookupPos = p * noiseScale + vec3(0, 0, time * 0.1);
	return texture(u_curlTexture, lookupPos).rgb;
}

float fbmCurlMagnitude(vec3 p, float time) {
	float noiseScale = 0.02;
	vec3  lookupPos = p * noiseScale + vec3(0, 0, time * 0.1);
	return texture(u_curlTexture, lookupPos).a;
}

// Simulation parameters
const float kExhaustSpeed = 30.0;
const float kExhaustSpread = 0.1;
const float kExhaustLifetime = 2.0;
const float kExhaustDrag = 1.5;

const float kExplosionSpeed = 30.0;
const float kExplosionLifetime = 2.5;
const float kExplosionDrag = 2.0;

const float kFireSpeed = 1.0;
const float kFireSpread = 4.2;
const float kFireLifetime = 5.0;

const float kSparksSpeed = 40.0;
const float kSparksLifetime = 0.8;
const float kSparksDrag = 1.0;
const float kSparksGravity = 15.0;

const float kGlitterSpeed = 25.0;
const float kGlitterSpread = 0.4;
const float kGlitterLifetime = 3.5;
const float kGlitterDrag = 1.2;
const float kGlitterGravity = 4.0;

layout(local_size_x = 256, local_size_y = 1, local_size_z = 1) in;

void main() {
	uint gid = gl_GlobalInvocationID.x;

	if (gid >= particles.length() || u_num_emitters == 0) {
		return;
	}

	Particle p = particles[gid];

	// Decrease lifetime
	p.pos.w -= u_delta_time;

	// If particle is dead, try to respawn it
	if (p.pos.w <= 0.0) {
		// Look up the assigned emitter from the indirection map
		int emitter_index = particle_to_emitter_map[gid];

		// If the emitter index is -1, this particle is inactive
		if (emitter_index == -1 || emitter_index >= u_num_emitters) {
			p.pos.w = 0.0; // Ensure it stays dead
			particles[gid] = p;
			return;
		}

		Emitter emitter = emitters[emitter_index];
		if (emitter.is_active == 0) {
			p.pos.w = 0.0; // Emitter is no longer active
			particles[gid] = p;
			return;
		}

		p.emitter_index = emitter_index;
		p.emitter_id = emitter.id;
		p.style = emitter.style;
		p.epicenter = emitter.position;

		vec2 seed = vec2(float(gid), u_time);
		vec3 random_vec = rand3(seed) * 2.0 - 1.0;

		float lifeTimeModifier = rand(seed); // * 0.25 + 0.75;
		// Initialize based on style
		if (p.style == 0) { // MissileExhaust
			p.pos = vec4(emitter.position, kExhaustLifetime * lifeTimeModifier);
			vec3 perpendicular = normalize(cross(emitter.direction, random_vec));
			vec3 spread_dir = mix(emitter.direction, perpendicular, kExhaustSpread * rand(seed.yx));
			p.vel = vec4(emitter.velocity + normalize(spread_dir) * kExhaustSpeed * rand(seed), 0.0);
		} else if (p.style == 1) { // Explosion
			p.pos = vec4(emitter.position, kExplosionLifetime * lifeTimeModifier);
			p.vel = vec4(normalize(random_vec) * kExplosionSpeed * 0.5 + rand(seed.yx), 0.0);
		} else if (p.style == 2) { // Fire
			p.pos = vec4(emitter.position + random_vec * 0.1, kFireLifetime * lifeTimeModifier);
			p.vel = vec4(0, kFireSpeed, 0, 0); // Start with upward velocity
		} else if (p.style == 3) {             // Sparks
			p.pos = vec4(emitter.position, kSparksLifetime * (rand(seed) * 0.5 + 0.5));
			p.vel = vec4(emitter.velocity + normalize(random_vec) * kSparksSpeed * rand(seed.yx), 0.0);
		} else if (p.style == 4) { // Glitter
			p.pos = vec4(emitter.position, kGlitterLifetime * lifeTimeModifier);
			vec3 dir = length(emitter.direction) > 0.001 ? normalize(emitter.direction) : vec3(0, 1, 0);
			vec3 spread_dir = normalize(dir + (random_vec * kGlitterSpread));
			p.vel = vec4(emitter.velocity + spread_dir * kGlitterSpeed * (0.5 + 0.5 * rand(seed)), 0.0);
		}
	}
	// If particle is alive, update it
	else {
		float maxSpeed;
		if (p.style == 0) {
			maxSpeed = kExhaustSpeed;
			p.vel.xyz -= p.vel.xyz * kExhaustDrag * 2 * (1 - (p.vel.xyz / 30)) * u_delta_time;
		} else if (p.style == 1) { // Explosion
			maxSpeed = kExplosionSpeed;
			p.vel.xyz -= p.vel.xyz * kExplosionDrag * u_delta_time;

			// Apply distance-based curl noise
			float   dist = distance(p.pos.xyz, p.epicenter);
			float   curlInfluence = smoothstep(5.0, 30.0, dist);
			p.vel.xyz += curlNoise(p.pos.xyz, u_time) * curlInfluence * 15.0 * u_delta_time;
		} else if (p.style == 2) { // Fire
			maxSpeed = kFireSpeed;
			p.vel.y += kFireSpeed * u_delta_time;
			p.vel.x += (rand(p.pos.xy + u_time) - 0.5) * kFireSpread * u_delta_time;
			p.vel.z += (rand(p.pos.yz + u_time) - 0.5) * kFireSpread * u_delta_time;
		} else if (p.style == 3) { // Sparks
			maxSpeed = kSparksSpeed;
			p.vel.xyz -= p.vel.xyz * kSparksDrag * u_delta_time;
			p.vel.y -= kSparksGravity * u_delta_time;
		} else if (p.style == 4) { // Glitter
			maxSpeed = kGlitterSpeed;
			p.vel.xyz -= p.vel.xyz * kGlitterDrag * u_delta_time;
			p.vel.y -= kGlitterGravity * u_delta_time;
		}

		if (p.style != 3 && p.style != 4) {
			p.vel.y -= 0.05 * u_delta_time;
		}

		p.vel += vec4(mix(curlNoise(p.pos.xyz, u_time) * 3, vec3(0, 0, 0), length(p.vel) / maxSpeed), 0) * u_delta_time;

		p.pos.xyz += p.vel.xyz * u_delta_time;

		// Terrain collision and reflection
		bool collided = false;
		for (int i = 0; i < u_num_chunks; i++) {
			ChunkInfo chunk = chunks[i];
			if (p.pos.x >= chunk.worldOffset.x && p.pos.x < chunk.worldOffset.x + chunk.size &&
			    p.pos.z >= chunk.worldOffset.y && p.pos.z < chunk.worldOffset.y + chunk.size) {
				vec2  uv = (p.pos.xz - chunk.worldOffset) / chunk.size;
				vec4  terrain = texture(u_heightmapArray, vec3(uv, chunk.slice));
				float height = terrain.r;
				vec3  normal = vec3(terrain.g, terrain.b, terrain.a); // Normal is already normalized in heightmap

				if (p.pos.y < height) {
					p.pos.y = height + 0.05;
					p.vel.xyz = reflect(p.vel.xyz, normal) * 0.4;
					collided = true;
				}
				break;
			}
		}

		// Fallback to flat ground if no chunk found or if below absolute zero
		if (!collided && p.pos.y <= 0.0) {
			p.pos.y = 0.1;
			p.vel.y *= -0.25;
			p.vel.xz *= 0.8;
		}
	}

	particles[gid] = p;
}
