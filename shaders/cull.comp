#version 430 core

layout(local_size_x = 64) in;

struct InstanceData {
    mat4 modelMatrix;
};

layout(std430, binding = 0) readonly buffer InstanceBuffer {
    InstanceData instances[];
};

layout(std430, binding = 1) writeonly buffer VisibleInstanceBuffer {
    uint visibleIndices[];
};

layout(std430, binding = 2) buffer DrawCommandBuffer {
    uint count;
    uint instanceCount;
    uint firstIndex;
    uint baseVertex;
    uint baseInstance;
} drawCommand;

#include "frustum.glsl"

uniform uint numInstances;
uniform float boundingRadius;

void main() {
    uint idx = gl_GlobalInvocationID.x;
    if (idx >= numInstances) return;

    mat4 modelMatrix = instances[idx].modelMatrix;
    vec3 center = vec3(modelMatrix[3]);
    float scale = length(vec3(modelMatrix[0]));
    float effectiveRadius = boundingRadius * scale;

    if (isSphereInFrustum(center, effectiveRadius)) {
        uint visibleIdx = atomicAdd(drawCommand.instanceCount, 1);
        visibleIndices[visibleIdx] = idx;
    }
}
