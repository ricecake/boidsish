#version 430 core

layout(local_size_x = 8, local_size_y = 8, local_size_z = 1) in;

// Source: previous mip level (or depth buffer for mip 0)
uniform sampler2D u_srcDepth;

// Destination: current mip level being generated
layout(r32f, binding = 0) uniform writeonly image2D u_dstMip;

// Source dimensions (for UV calculation)
uniform ivec2 u_srcSize;

void main() {
	ivec2 pixel = ivec2(gl_GlobalInvocationID.xy);
	ivec2 dstSize = imageSize(u_dstMip);

	if (pixel.x >= dstSize.x || pixel.y >= dstSize.y)
		return;

	// Each destination pixel covers a 2x2 block in the source
	vec2 srcTexelSize = 1.0 / vec2(u_srcSize);
	vec2 uv = (vec2(pixel) * 2.0 + 1.0) * srcTexelSize;

	float d00 = texture(u_srcDepth, uv + vec2(-0.5, -0.5) * srcTexelSize).r;
	float d10 = texture(u_srcDepth, uv + vec2(0.5, -0.5) * srcTexelSize).r;
	float d01 = texture(u_srcDepth, uv + vec2(-0.5, 0.5) * srcTexelSize).r;
	float d11 = texture(u_srcDepth, uv + vec2(0.5, 0.5) * srcTexelSize).r;

	// MAX for conservative occlusion testing
	float maxDepth = max(max(d00, d10), max(d01, d11));

	imageStore(u_dstMip, pixel, vec4(maxDepth));
}
