#version 410 core
layout(triangles, equal_spacing, ccw) in;

in vec3 vs_color_in[];
out vec3 FragPos;
out vec3 Normal;
out vec3 vs_color;

uniform mat4 model;
uniform mat4 view;
uniform mat4 projection;
uniform vec4 clipPlane;
uniform float time;
uniform float ripple_strength;

uniform vec3 lightPos;
uniform vec3 viewPos;
uniform vec3 lightColor;

uniform vec3 objectColor;
uniform int  useVertexColor;

vec3 interpolate(vec3 v0, vec3 v1, vec3 v2) {
    return v0 * gl_TessCoord.x + v1 * gl_TessCoord.y + v2 * gl_TessCoord.z;
}

void main() {
    vec3 p0 = gl_in[0].gl_Position.xyz;
    vec3 p1 = gl_in[1].gl_Position.xyz;
    vec3 p2 = gl_in[2].gl_Position.xyz;

    vec3 pos = interpolate(p0, p1, p2);

    float k = 2.0;
    float speed = 4.0;
    float amplitude = 0.1 * ripple_strength;

    float wave = amplitude * sin(k * pos.x + speed * time) + amplitude * cos(k * pos.z + speed * time * 0.5);
    pos.y += wave;

    float dx = amplitude * k * cos(k * pos.x + speed * time);
    float dz = -amplitude * k * sin(k * pos.z + speed * time * 0.5);
    vec3 tangent = normalize(vec3(1.0, dx, 0.0));
    vec3 bitangent = normalize(vec3(0.0, dz, 1.0));
    vec3 normal = cross(bitangent, tangent);

    FragPos = vec3(model * vec4(pos, 1.0));
    Normal = mat3(transpose(inverse(model))) * normal;
    vs_color = vs_color_in[0];
    gl_Position = projection * view * vec4(FragPos, 1.0);
    gl_ClipDistance[0] = dot(vec4(FragPos, 1.0), clipPlane);
}