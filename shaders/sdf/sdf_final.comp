#version 430 core
layout(local_size_x = 4, local_size_y = 4, local_size_z = 4) in;

layout(rgba32f, binding = 0) uniform image3D u_seed_texture;
layout(rgba32f, binding = 1) uniform image3D u_normal_texture;
layout(rgba32f, binding = 2) uniform image3D u_final_texture;

uniform int  u_grid_res;
uniform vec3 u_min_bounds;
uniform vec3 u_max_bounds;

void main() {
	ivec3 vox = ivec3(gl_GlobalInvocationID);
	if (any(greaterThanEqual(vox, ivec3(u_grid_res))))
		return;

	vec3 size = u_max_bounds - u_min_bounds;
	vec3 voxel_size = size / float(u_grid_res);
	vec3 vox_center_world = u_min_bounds + (vec3(vox) + 0.5) * voxel_size;

	vec4 seed = imageLoad(u_seed_texture, vox);
	vec4 normal = imageLoad(u_normal_texture, vox);

	if (seed.w < 0.5) {
		imageStore(u_final_texture, vox, vec4(1000.0, 0, 0, 1.0));
		return;
	}

	vec3  to_seed_world = seed.xyz - vox_center_world;
	float dist = length(to_seed_world);

	// Determine sign: dot(vector_from_seed_to_vox, normal)
	vec3  from_seed_to_vox = -to_seed_world;
	float sign = dot(from_seed_to_vox, normal.xyz) >= 0.0 ? 1.0 : -1.0;

	imageStore(u_final_texture, vox, vec4(dist * sign, normal.xyz));
}
