#version 430 core

struct Fragment {
	vec4 v0;
	vec4 v1;
	vec4 v2;
	vec4 t01;
	vec4 t2_age;
	vec4 normal;
	vec4 pos;
	vec4 vel;
	vec4 rot;
	vec4 angVel;
	vec4 color;
};

layout(std430, binding = 0) buffer FragmentBuffer {
	Fragment fragments[];
};

uniform float u_delta_time;
uniform float u_time;

layout(local_size_x = 64, local_size_y = 1, local_size_z = 1) in;

vec4 quat_mul(vec4 q1, vec4 q2) {
	return vec4(
		q1.w * q2.x + q1.x * q2.w + q1.y * q2.z - q1.z * q2.y,
		q1.w * q2.y - q1.x * q2.z + q1.y * q2.w + q1.z * q2.x,
		q1.w * q2.z + q1.x * q2.y - q1.y * q2.x + q1.z * q2.w,
		q1.w * q2.w - q1.x * q2.x - q1.y * q2.y - q1.z * q2.z
	);
}

void main() {
	uint gid = gl_GlobalInvocationID.x;
	if (gid >= fragments.length())
		return;

	if (fragments[gid].t2_age.w <= 0.0)
		return; // Not active

	fragments[gid].t2_age.z += u_delta_time; // age
	float age = fragments[gid].t2_age.z;
	float lifetime = fragments[gid].t2_age.w;

	if (age >= lifetime) {
		fragments[gid].t2_age.w = 0.0; // Mark as dead
		return;
	}

	// Physics
	fragments[gid].pos.xyz += fragments[gid].vel.xyz * u_delta_time;
	fragments[gid].vel.y -= 9.8 * u_delta_time;           // Gravity
	fragments[gid].vel.xyz *= (1.0 - 0.5 * u_delta_time); // Drag

	// Rotation
	vec3  axis = fragments[gid].angVel.xyz;
	float speed = fragments[gid].angVel.w;
	float angle = speed * u_delta_time;
	vec4  delta_rot = vec4(axis * sin(angle * 0.5), cos(angle * 0.5));
	fragments[gid].rot = quat_mul(delta_rot, fragments[gid].rot);
	fragments[gid].rot = normalize(fragments[gid].rot);

	// Alpha fade
	fragments[gid].color.a = clamp(1.0 - (age / lifetime), 0.0, 1.0);
}
