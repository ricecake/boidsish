#version 430 core

layout(local_size_x = 16, local_size_y = 16) in;

layout(rgba8, binding = 4) uniform writeonly image2D weatherMap;

// 2D Hash
vec2 hash22(vec2 p) {
	p = vec2(dot(p, vec2(127.1, 311.7)), dot(p, vec2(269.5, 183.3)));
	return fract(sin(p) * 43758.5453123);
}

float noise(vec2 p) {
	vec2 i = floor(p);
	vec2 f = fract(p);
	vec2 u = f * f * (3.0 - 2.0 * f);

	return mix(mix(dot(hash22(i + vec2(0.0, 0.0)), f - vec2(0.0, 0.0)),
	               dot(hash22(i + vec2(1.0, 0.0)), f - vec2(1.0, 0.0)),
	               u.x),
	           mix(dot(hash22(i + vec2(0.0, 1.0)), f - vec2(0.0, 1.0)),
	               dot(hash22(i + vec2(1.0, 1.0)), f - vec2(1.0, 1.0)),
	               u.x),
	           u.y);
}

float fbm(vec2 p) {
	float value = 0.0;
	float amplitude = 0.5;
	for (int i = 0; i < 6; i++) {
		value += amplitude * noise(p);
		p *= 2.0;
		amplitude *= 0.5;
	}
	return value * 0.5 + 0.5;
}

void main() {
	ivec2 texel = ivec2(gl_GlobalInvocationID.xy);
	ivec2 size = imageSize(weatherMap);
	vec2  uv = (vec2(texel) + 0.5) / vec2(size);

	float coverage = fbm(uv * 3.0);
	float type = fbm(uv * 5.0 + 10.0);
	float height = fbm(uv * 2.0 + 20.0);

	imageStore(weatherMap, texel, vec4(coverage, type, height, 1.0));
}
