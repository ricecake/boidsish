#version 430 core
layout(local_size_x = 8, local_size_y = 8) in;

layout(binding = 0, r16f) uniform image2D outResult;
layout(binding = 1, r32f) uniform image2D outDepth;

uniform sampler2D uCurrentFrame;
uniform sampler2D uHistoryFrame;
uniform sampler2D uVelocity;
uniform sampler2D uDepth;
uniform sampler2D uHistoryDepth;

#include "temporal_data.glsl"

uniform float uAlpha = 0.9;

float linearizeDepth(float d) {
    float z = d * 2.0 - 1.0;
    return (2.0 * 0.1 * 1000.0) / (1000.0 + 0.1 - z * (1000.0 - 0.1));
}

void main() {
	ivec2 pixel = ivec2(gl_GlobalInvocationID.xy);
	ivec2 size = imageSize(outResult);
	if (pixel.x >= size.x || pixel.y >= size.y)
		return;

	vec2 uv = (vec2(pixel) + 0.5) / vec2(size);
    float currentDepth = texture(uDepth, uv).r;

	vec2 velocity = texture(uVelocity, uv).rg;
	vec2 prevUV = uv - velocity;

	float current = texture(uCurrentFrame, uv).r;
    if (isnan(current) || isinf(current)) current = 0.0;
    current = clamp(current, 0.0, 10.0);

	if (prevUV.x < 0.0 || prevUV.x > 1.0 || prevUV.y < 0.0 || prevUV.y > 1.0) {
		imageStore(outResult, pixel, vec4(current, 0, 0, 0));
        imageStore(outDepth, pixel, vec4(currentDepth));
		return;
	}

	float history = texture(uHistoryFrame, prevUV).r;
    if (isnan(history) || isinf(history)) history = current;

    float historyDepth = texture(uHistoryDepth, prevUV).r;

    float currLin = linearizeDepth(currentDepth);
    float histLin = linearizeDepth(historyDepth);
    bool disoccluded = abs(currLin - histLin) > (currLin * 0.05 + 0.1);

	float m1 = 0.0;
	float m2 = 0.0;
    float minVal = 10000.0;
    float maxVal = -10000.0;

	for (int y = -1; y <= 1; y++) {
		for (int x = -1; x <= 1; x++) {
			float val = texture(uCurrentFrame, uv + vec2(x, y) * texelSize).r;
            if (isnan(val)) val = current;
			m1 += val;
			m2 += val * val;
            minVal = min(minVal, val);
            maxVal = max(maxVal, val);
		}
	}
	float mean = m1 / 9.0;
	float stddev = sqrt(max(0.0, (m2 / 9.0) - (mean * mean)));

	float gamma = 1.25;
	float cl_min = mean - gamma * stddev;
	float cl_max = mean + gamma * stddev;

    minVal = max(minVal, cl_min);
    maxVal = min(maxVal, cl_max);

	history = clamp(history, minVal, maxVal);

    float edgeFactor = smoothstep(0.0, 0.05, prevUV.x) * smoothstep(1.0, 0.95, prevUV.x) *
                       smoothstep(0.0, 0.05, prevUV.y) * smoothstep(1.0, 0.95, prevUV.y);

	float activeAlpha = disoccluded ? 0.0 : (uAlpha * edgeFactor);
	float result = mix(current, history, activeAlpha);

	imageStore(outResult, pixel, vec4(result, 0, 0, 0));
    imageStore(outDepth, pixel, vec4(currentDepth));
}
