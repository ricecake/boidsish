#version 430 core

layout(local_size_x = 8, local_size_y = 8) in;

layout(binding = 0) uniform sampler2D u_InputDepth;
layout(binding = 1, r32f) uniform writeonly image2D u_OutputDepth;

uniform vec2 u_DestSize;
uniform int u_SrcLevel;
uniform int u_Mode; // 0: Copy Level 0, 1: Downsample

void main() {
    ivec2 destPos = ivec2(gl_GlobalInvocationID.xy);
    ivec2 size = ivec2(u_DestSize);
    if (destPos.x >= size.x || destPos.y >= size.y) {
        return;
    }

    if (u_Mode == 0) {
        float d = texelFetch(u_InputDepth, destPos, 0).r;
        imageStore(u_OutputDepth, destPos, vec4(d));
        return;
    }

    ivec2 srcPos = destPos * 2;
    float d0, d1, d2, d3;

    d0 = texelFetch(u_InputDepth, srcPos + ivec2(0, 0), u_SrcLevel).r;
    d1 = texelFetch(u_InputDepth, srcPos + ivec2(1, 0), u_SrcLevel).r;
    d2 = texelFetch(u_InputDepth, srcPos + ivec2(0, 1), u_SrcLevel).r;
    d3 = texelFetch(u_InputDepth, srcPos + ivec2(1, 1), u_SrcLevel).r;

    float minDepth = min(min(d0, d1), min(d2, d3));
    imageStore(u_OutputDepth, destPos, vec4(minDepth));
}
