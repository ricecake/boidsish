#version 430 core
layout(local_size_x = 64) in;

#include "frustum.glsl"

layout(std430, binding = 0) readonly buffer InputMatrices {
	mat4 inputMatrices[];
};

layout(std430, binding = 1) writeonly buffer OutputMatrices {
	mat4 outputMatrices[];
};

layout(std430, binding = 3) buffer AtomicCounter {
	uint visibleCount;
};

layout(std430, binding = 4) readonly buffer InputColors {
	vec4 inputColors[];
};

layout(std430, binding = 5) writeonly buffer OutputColors {
	vec4 outputColors[];
};

uniform int   u_numInstances;
uniform float u_radius;
uniform bool  u_hasColors;

void main() {
	uint idx = gl_GlobalInvocationID.x;
	if (idx >= u_numInstances)
		return;

	mat4  modelMatrix = inputMatrices[idx];
	vec3  center = vec3(modelMatrix[3]);
	float scale = length(vec3(modelMatrix[0]));
	float effectiveRadius = u_radius * scale;

	if (isSphereInFrustum(center, effectiveRadius)) {
		uint outputIdx = atomicAdd(visibleCount, 1);
		outputMatrices[outputIdx] = modelMatrix;
		if (u_hasColors) {
			outputColors[outputIdx] = inputColors[idx];
		}
	}
}
